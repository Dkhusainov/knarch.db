/*
 * Copyright (c) 2018 Touchlab Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_native_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'konan'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'co.touchlab.knarch'
version = "0.5-alpha1"

ext.bintrayGitUrl = 'https://github.com/touchlab/knarch'

apply from: "https://raw.githubusercontent.com/doppllib/gradlescripts/master/kotlinnativepublish.gradle"
apply from: "https://raw.githubusercontent.com/doppllib/gradlescripts/master/kotlinbintray.gradle"

konan.targets = ['iphone', 'iphone_sim', 'macos_x64']

konanArtifacts {
    library("knarch"){
        target('iphone_sim') {
            nativeLibraries '../../cpp/bcdist/ios_x64/tlruntime.bc'
        }

        target('iphone') {
            nativeLibraries '../../cpp/bcdist/ios_arm64/tlruntime.bc'
        }

        target('macos_x64') {
            nativeLibraries '../../cpp/bcdist/macos_x64/tlruntime.bc'
        }

        enableMultiplatform true
    }

    //TODO: Rename test framework. Just feels like a lot of Xcode work that I don't want.
    framework('knarchtest', targets: ['iphone', 'iphone_sim']) {
        srcDir 'src/main/kotlin'
        srcDir 'src/test/kotlin'

        target('iphone_sim') {
            nativeLibraries '../../cpp/bcdist/ios_x64/tlruntime.bc'
        }

        target('iphone') {
            nativeLibraries '../../cpp/bcdist/ios_arm64/tlruntime.bc'
        }

        extraOpts "-tr", "-g"
        linkerOpts "-lsqlite3"
        enableMultiplatform true
    }

    program("knarchtestcmd", targets: ['macos_x64']) {
        srcDir 'src/main/kotlin'
        srcDir 'src/test/kotlin'
        srcDir 'src/testexe/kotlin'

        target('macos_x64') {
            nativeLibraries '../../cpp/bcdist/macos_x64/tlruntime.bc'
        }

        extraOpts "-tr", "-g"
        linkerOpts "-lsqlite3"
        enableMultiplatform true
    }
}

dependencies {
    expectedBy project(':knarch-common')
}

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}
